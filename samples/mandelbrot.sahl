// ./sahl exe.bin > image.pgm
// display image.pgm

// takes about 2 minutes

fun sqr(a: double) -> double {
    return a * a;
}

// regs is 4x2 array of doubles
fun interpret(regs: [[double]], code: [char]) {
    let i = 0;
    let l = len(code);
    let a_charcode = cast('a', int);
    while i < l {
        let c1 = cast(code[i + 1], int);
        let src = regs[c1 - a_charcode];
        let c2 = cast(code[i + 2], int);
        let dest = regs[c2 - a_charcode];
        if code[i] == '=' {
            dest[0] = src[0];
            dest[1] = src[1];
        } else if code[i] == '+' {
            dest[0] = dest[0] + src[0];
            dest[1] = dest[1] + src[1];
        } else if code[i] == '*' {
            let r = (dest[0] * src[0]) - (dest[1] * src[1]);
            let i = (dest[0] * src[1]) + (dest[1] * src[0]);
            dest[0] = r;
            dest[1] = i;
        }
        i = i + 3;
    }
}

fun main() {
    let registers = [[0.0, 0.0], [0.0, 0.0], [0.0, 0.0], [0.0, 0.0]];
    let i = 0;
    let x = 0;
    let y = 0;
    let code = ['*', 'b', 'b', '+', 'a', 'b'];
    let width = 180;
    let height = 200;
    let w_float = cast(width, double);
    let h_float = cast(height, double);
    while y < height {
        x = 0;
        let line = make([char], width);
        while x < width {
            registers[0][0] = 2.0 * (h_float / 100.0) * (cast(x, double) / h_float - 0.5);
            registers[0][1] = 2.0 * (w_float / 100.0) * (cast(y, double) / w_float - 0.5);
            i = 1;
            while i < 4 {
                registers[i][0] = 0.0;
                registers[i][1] = 0.0;
                i = i + 1;
            }
            i = 0;
            while i < 256 && ((sqr(registers[1][0]) + sqr(registers[1][1])) < 4.0) {
                interpret(registers, code);
                i = i + 1;
            }
            line[x] = cast(i, char);
            x = x + 1;
        }
        for c in line {
            let c_int = cast(c, int);
            // pinkish
            let color = 12 + (c_int % 12);
            print("\u001b[38;5;", color, "m", "o");
        }
        print("\u001b[0m\n");
        y = y + 1;
    }
}